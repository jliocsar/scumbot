* as fs from node:fs/promises
* as path from node:path
{ fileURLToPath } from node:url
{ type SlashCommandBuilder, Collection } from discord.js
type { CommandHandler } from ./types.civet

__filename := fileURLToPath import.meta.url
__dirname := path.dirname __filename

export class CommandRouter
  private readonly commandsPath = path.join __dirname, 'commands'
  private routes: Record<string, string> = {}

  private loadRoutes()
    files := await fs.readdir @commandsPath
    for file of files
      unless @isInternalFile file
        name := file.replace '.civet', ''
        filePath := path.join 'commands', file
        @routes[name] = filePath

  loadCommands()
    await @loadRoutes()
    applicationCommands: SlashCommandBuilder[] := []
    commands := new Collection<string, CommandHandler>
    commandsRoutes := Object.entries @routes
    for [fileName, filePath] of commandsRoutes
      if @isInternalFile filePath
        continue
      absFilePath := path.resolve __dirname, filePath
      module := await import absFilePath
      handler := module.default as CommandHandler
      unless handler
        console.warn `No default export found in ${filePath}`
        continue
      name := fileName.replace '/', ''
      commands.set name, handler
      if command := module.command
        applicationCommands.push command
      else
        console.warn `No command found in ${filePath}, this command will not be registered`
    { commands, applicationCommands }

  private isInternalFile(filePath: string)
    filePath.startsWith '/_'
