{ createAudioPlayer, AudioPlayerStatus, getVoiceConnection } from @discordjs/voice
{ playAudioResource } from ./helpers/audio-resource.civet
{ voiceMachineActor } from ./machines/voice.civet
{ client } from ./client.civet

TIMEOUT_DURATION_MS := 30_000

export audioPlayer := createAudioPlayer()

audioPlayer.on AudioPlayerStatus.Idle, (oldState) ->
  return unless 'resource' in oldState
  resource := oldState.resource
  { resources, queues, timeouts } := voiceMachineActor.getSnapshot().context
  guildId := resources.get resource
  return unless guildId
  voiceMachineActor.send { type: 'voice.idling', guildId }
  queue := queues.get guildId
  return unless queue
  { data: next } := queue.next()
  if timeouts.has guildId
    clearTimeout timeouts.get guildId
  unless next
    return timeouts.set
      guildId,
      setTimeout ->
        guildId |> getVoiceConnection |> ?.destroy()
        voiceMachineActor.send { type: 'voice.destroy', guildId }
      , TIMEOUT_DURATION_MS
  { data: embed } := await playAudioResource guildId, next.info
  unless embed
    return voiceMachineActor.send { type: 'voice.destroy', guildId }
  channel := client.channels.cache.get next.channelId
  if channel?.isTextBased()
    channel.send {
      content: 'ðŸŽ§ Changing to'
      embeds: [embed]
    }
