import dotenv/config
{ Events, ActivityType } from discord.js

{ TOKEN } from ./const.civet
{ CommandRouter } from ./router.civet
{ client } from ./client.civet
{ registerApplicationCommands } from ./register.civet

router := new CommandRouter
{ commands, applicationCommands } := await router.loadCommands()

await registerApplicationCommands applicationCommands

client
  ||> .on Events.ClientReady, ->
    console.info 'Scumbot is ready'
    client.user!.setActivity {
      name: 'your mom sunbathing ðŸ˜Ž'
      type: ActivityType.Watching
    }
    return
  ||> .on Events.InteractionCreate, (interaction) ->
    return unless interaction.isChatInputCommand()
    if command := commands.get interaction.commandName
      await command interaction
      unless interaction.replied or interaction.deferred
        await interaction.reply { content: 'Command failed', +ephemeral }
    else
      await interaction.reply { content: 'Unknown command', +ephemeral }
    return

await client.login TOKEN
