{ type GuildMember, SlashCommandBuilder } from discord.js
{ getVoiceConnection } from @discordjs/voice
type { CommandHandler } from ../types.civet
{ voiceMachineActor } from ../machines/voice.civet
{ playAudioResource } from ../helpers/audio-resource.civet

export command := new SlashCommandBuilder()
  .setName 'skip'
  .setDescription 'Skips the current song.'

export default skip: CommandHandler := (interaction) ->
  [guild, member] := [interaction.guild, interaction.member]
  guildMember := member as GuildMember
  unless guildMember.voice.channel
    return interaction.reply 'You need to be in a voice channel to skip the music'
  guildId := guild!.id
  currentConnection := getVoiceConnection guildId
  unless currentConnection
    return interaction.reply 'I am not connected to any/your voice channel'
  queue := voiceMachineActor
    |> .getSnapshot().context
    |> .queues
    |> .get guildId
  unless queue
    return interaction.reply 'I am not playing any music'
  if queue.isEmpty
    return interaction.reply 'There is no song to skip'
  { data: next } := queue.next()
  unless next
    return interaction.reply 'There is no song to skip'
  await playAudioResource guildId, next.info
  interaction.reply { content: 'ðŸ‘Œ', +ephemeral }
