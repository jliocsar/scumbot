{
  type GuildMember
  SlashCommandBuilder
  EmbedBuilder
} from discord.js
{ VoiceConnectionStatus } from @discordjs/voice
playdl from play-dl
type { CommandHandler } from ../types.civet
{ audioPlayer } from ../audio-player.civet
{ playAudioResource, joinGuildMemberVoiceChannel, queueSong } from ../helpers/audio-resource.civet

export command := new SlashCommandBuilder()
  .setName 'play'
  .setDescription 'Plays a YouTube song'
  .addStringOption
    &.setName 'search'
    |> .setDescription 'The search or URL of the song to play'
    |> .setRequired true
  .addIntegerOption
    &.setName 'time'
    |> .setDescription 'The `t` time to start the song at'
    |> .setRequired false

function getVideoInfo(search: string, time: number | null)
  url .= search
  isUrl := /^(https:\/\/)?youtu\.?be/.test url
  unless isUrl
    [video] := await playdl.search search, { limit: 1 }
    url = video.url
  urlInstance := new URL url
  seek := Number(urlInstance.searchParams.get 't') || time
  info := await playdl.video_info url
  { info, seek }

export default play: CommandHandler := (interaction) ->
  search .= interaction.options.getString 'search'
  time := interaction.options.getInteger 'time'
  unless search
    return interaction.reply { content: 'You need to provide a search or URL to play', +ephemeral }
  try
    await interaction.reply 'ðŸ” Searching for the song...'
    { info, seek } := await getVideoInfo search, time
    [guild, member] := [interaction.guild, interaction.member]
    guildId := guild!.id
    { data: connection, error } := joinGuildMemberVoiceChannel member as GuildMember, guild
    if error
      errorMessage .= error
      if error is 'Currently playing a song'
        queueSong guildId, interaction.channelId, info
        errorMessage += ' -- The song has been added to the queue'
      return interaction.editReply errorMessage
    if subscription := connection!.subscribe audioPlayer
      connection!.on VoiceConnectionStatus.Ready, ->
        { data: embed } := await playAudioResource guildId, info, seek
        interaction.editReply {
          content: 'ðŸŽ§ Now playing'
          embeds: [embed]
        }
  catch (error)
    interaction.editReply content: (error as Error).message
